apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
// JitPack Maven
apply plugin: 'com.github.dcendents.android-maven'
// Your Group
group = 'com.github.bayescom'

//maven依赖的项目名称
def pomName = "tap"
def extVersionName = ""
def libVersionName = "3.16.3.47"
def advanceVersionName = "${rootProject.ext.advance_version}_" + libVersionName

advanceVersionName = advanceVersionName + extVersionName

def outputName = pomName + "_" + advanceVersionName + ".aar"


android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    println "advanceVersionName = " + advanceVersionName

}

repositories {
    flatDir {
        dirs 'libs'
    }
}


dependencies {

    implementation 'androidx.appcompat:appcompat:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    def name = "TapAD_" + libVersionName+ '.aar'
    if (IS_PUBLISH.toBoolean()){
        compileOnly fileTree(dir: 'libs', include: [name])
    }else {
        api fileTree(dir: 'libs', include: [name])
    }

    compileOnly project(path: ':advancelib')
    compileOnly "io.github.bayescom:mercury-sdk:${rootProject.ext.mercury_version}"


}


android.libraryVariants.all { variant ->
    variant.outputs.all {
        outputFileName = outputName
    }
}



afterEvaluate {
    publishing {
        publications {
            // 创建一个发布任务，名字随意，比如 'release'
            release(MavenPublication) {
                // 1. 自动从 release版本 提取组件
                from components.release

                // 2. 自定义 ArtifactId (这就是用户引用时的名字)
                // 如果不写，默认就是当前 module 的文件夹名
                artifactId = pomName

                // 3. GroupId 通常由 JitPack 自动覆盖，但建议写上
                groupId = group

                // 4. 版本号
                version = "${rootProject.ext.advance_version}"
            }
        }
    }
}
