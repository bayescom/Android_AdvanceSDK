apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
// JitPack Maven
apply plugin: 'com.github.dcendents.android-maven'
// Your Group
group = 'com.github.bayescom'


//maven依赖的项目名称
def pomName = "honor"
def extVersionName = ""
def libVersionName = "111"
def advanceVersionName = "${rootProject.ext.advance_version}_" + libVersionName

def advanceVersionCode = 1


advanceVersionName = advanceVersionName + extVersionName

def outputName = pomName + "_" + advanceVersionName + ".aar"

android {
    compileSdkVersion 31
    buildToolsVersion "29.0.3"


    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 29
        versionCode advanceVersionCode
        versionName advanceVersionName

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    println "advanceVersionName = " + advanceVersionName

}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    def honor_ad_sdk_version = "1.0.32.300"
    api "com.hihonor.mcs:ads-lite:$honor_ad_sdk_version"
    api "com.hihonor.mcs:ads-tools:$honor_ad_sdk_version" // 如果您需要使用自测工具，可以引用该包

    compileOnly project(path: ':advancelib')
    compileOnly "io.github.bayescom:mercury-sdk:${rootProject.ext.mercury_version}"

    if (IS_PUBLISH.toBoolean()) {
        // 强制使用1.5.0版本，避免使用1.7.0（导致编译报错：android:attr/lStar not found）
        implementation('androidx.core:core:1.5.0') {
            force = true
        }
    }
}


afterEvaluate {
    publishing {
        publications {
            // 创建一个发布任务，名字随意，比如 'release'
            release(MavenPublication) {
                // 1. 自动从 release版本 提取组件
                from components.release

                // 2. 自定义 ArtifactId (这就是用户引用时的名字)
                // 如果不写，默认就是当前 module 的文件夹名
                artifactId = pomName

                // 3. GroupId 通常由 JitPack 自动覆盖，但建议写上
                groupId = group

                // 4. 版本号
                version = "${rootProject.ext.advance_version}"
            }
        }
    }
}
